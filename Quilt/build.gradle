plugins {
	id 'org.quiltmc.loom' version '1.+'
	id 'maven-publish'
}

archivesBaseName = "${mod_name}-quilt-${minecraft_version}"

repositories {

	maven {
		name 'Quilt (Release)'
		url 'https://maven.quiltmc.org/repository/release/'
	}

	maven {
		name 'Quilt (Snapshot)'
		url 'https://maven.quiltmc.org/repository/snapshot/'
		content {
			includeGroup 'org.quiltmc.fabric_api_qsl'
			includeGroupByRegex 'org\\.quiltmc\\.qsl(?:\\.[a-z_]+)?'
		}
	}
	maven { url = "https://maven.tterrag.com/" }
	maven { url = "https://mvn.devos.one/snapshots/"}
	maven { url = "https://jitpack.io/" }
}

dependencies {

	minecraft "com.mojang:minecraft:${minecraft_version}"
	mappings loom.officialMojangMappings()
	modImplementation "org.quiltmc:quilt-loader:${quilt_loader_version}"

	modImplementation("org.quiltmc:qsl:${quilt_stdlib_version}") {
		exclude group: "net.fabricmc"
	}

	modImplementation (include("org.quiltmc.quilt-kotlin-libraries:quilt-kotlin-libraries:${quilt_kotlin_verion}"))
	modImplementation "org.quiltmc.quilted-fabric-api:quilted-fabric-api:${quilt_fapi_version}-${minecraft_version}"
	modImplementation(include("com.tterrag.registrate_fabric:Registrate:${registrate_version}"))

	implementation group: 'com.google.code.findbugs', name: 'jsr305', version: '3.0.1'
	implementation project(":Common")
}

processResources {

    from project(":Common").sourceSets.main.resources
	inputs.property "version", project.version

	filesMatching("quilt.mod.json") {
		expand "version": project.version
	}
}

tasks.withType(JavaCompile) {

    source(project(":Common").sourceSets.main.allSource)
}

compileKotlin {
	source(project(":Common").sourceSets.main.kotlin)
}

sourcesJar {
	from project(":Common").sourceSets.main.kotlin
}

jar {
	from("LICENSE") {
		rename { "${it}_${mod_name}"}
	}
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId project.group
            artifactId project.archivesBaseName
            version project.version
            from components.java
        }
    }

    repositories {
        maven {
            url "file://" + System.getenv("local_maven")
        }
    }
}
compileKotlin {
	kotlinOptions {
		jvmTarget = "17"
	}
}
compileTestKotlin {
	kotlinOptions {
		jvmTarget = "17"
	}
}